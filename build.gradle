// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.2.21'
    ext.anko_version = '0.10.4'
    ext.android_support_version = "27.0.2"
    ext.android_lifecycle_version = '1.0.0'

    ext.libraries = [
        core: [
            fileTree(dir: 'libs', include: ['*.jar']),
            "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version",
        ],
        common: [
            'io.reactivex.rxjava2:rxjava:2.1.3',
            'io.reactivex.rxjava2:rxkotlin:2.1.0',
            'com.github.salomonbrys.kodein:kodein:4.1.0'
        ],
        unitTesting: [
            'junit:junit:4.12',
            'org.amshove.kluent:kluent:1.34',
            'org.mockito:mockito-core:2.8.47',
            "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
        ],
        ui: [
            project(":model"),
            "org.jetbrains.anko:anko-commons:$anko_version",
            "com.android.support:appcompat-v7:$android_support_version",
            "com.android.support:support-annotations:$android_support_version",
            'com.android.support.constraint:constraint-layout:1.0.2',
            'androidx.core:core-ktx:0.1',
            "android.arch.lifecycle:extensions:$android_lifecycle_version",
            'io.reactivex.rxjava2:rxandroid:2.0.1',
        ],
        uiTesting: [
            'com.android.support.test:runner:1.0.1',
            'com.android.support.test.espresso:espresso-core:3.0.1'
        ]
    ]

    repositories {
        google()
        jcenter()
        maven { url "http://dl.bintray.com/jetbrains/spek" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0-beta1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
